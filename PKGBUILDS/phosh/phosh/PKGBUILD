# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.37.1
pkgrel=2
pkgdesc="A Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL')
depends=(
    'callaudiod'
    'evince'
    'evolution-data-server'
    'feedbackd'
    'gcr'
    'gtk3'
    'libadwaita'
    'libcap'
    'libhandy'
    'libnm'
    'libpulse'
    'gnome-desktop'
    'gnome-session'
    'gnome-shell'
    'phoc'
    'squeekboard'
    'upower'
)
makedepends=(
    'git'
    'meson'
    'ninja'
    'wayland-protocols'
)
checkdepends=('xorg-server-xvfb')
install="$pkgname.install"
_commit="02635325804f8669860ea99f59bdf003ebe16a1b" # tags/v0.37.0
source=("phosh::git+https://gitlab.gnome.org/World/Phosh/phosh.git#commit=${_commit}"
	'1387.patch'
        'pam_phosh'
        'phosh.service')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd phosh
    git submodule update --init
    
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../$src"
    done

}

build() {
    arch-meson phosh output --libexecdir=/usr/lib/$pkgname -Dphoc_tests=disabled -Dsystemd=true
    ninja -C output
}

check() {
    xvfb-run ninja test -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    mkdir -p "$pkgdir"/usr/share/applications
    ln -s sm.puri.Squeekboard.desktop "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('SKIP'
	 '77cce2e020a83095ca467694419c4189'
         '6d5a0d561f8362bf91f85c236a88395d'
         '66663f2c9775d5f833cccd9c57b86e6e')
